#!/usr/bin/env osascript

# Note: We only test the aspect of not activating the new tab *within* Terminal here.
#       We're content to test the non-activation of Terminal itself in the -g test.

set ttabBin to "../bin/ttab"

# 1st test: create tab in *front* window and ensure the new tab doesn't become (technically: stay) the selected one.
tell application "Terminal"'s front window
    
  set tabCountBefore to count of tabs
  set thisTab to selected tab

  # Create tab in background.
  do shell script ttabBin & " -G"
 
  # !! Sleep a little to makes sure that the newly created tab is reflected
  # !! in `(count of tabs)` below. 
  delay 3
 
  if (count of tabs of it) = tabCountBefore then error "Failed to create tab in front window; still-current tab count: " & (count of tabs of it)

  # Ensure that the original tab is the selected  one (again)
  if not selected of thisTab then    
    set selected tab to thisTab # Re-select the previously active tab so that the error message shows.
    error "The originating tab unexpectedly did not remain the selected one."
  end if

  # Tabs don't understand `close`, but `do script "exit"` is preferable anyway (assuming the settings close the tab once the shell exits),
  # because it waits for the tab to be ready to process the command.
  do script "exit" in tab (count of tabs)

end tell

# 2nd test: create tab in *new* window and ensure the new tab window doesn't become (techincally: stay) active.
tell application "Terminal"
    
  set winCountBefore to count of windows

  set thisWin to front window

  # Create tab in new window without activating it.
  do shell script ttabBin & " -w -G"

  if not (count of windows) > winCountBefore then error "Failed to create new Terminal window."

  set newWin to window 2

  if not frontmost of thisWin then 
    set frontmost of thisWin to true
    error "This window unexpectedly did not remain the front window."
  end if

  # Close the new window, but we must wait for its shell to no longer be busy so as to avoid a 'really close?' prompt.
  activate it
  my closeTerminalWindow(newWin, 3)

end tell

return

# Synchronously activate the specified application. I.e., when this subroutine returns, the specified app is frontmost.
# Required, because on 10.10+ the activate command is *asynchronous*.
# Set TMOUT to adjust the timeout after which an error is raised.
on syncActivate(appName)
  local TMOUT, elapsedSoFar
  set TMOUT to 5 # secs.
  set elapsedSoFar to 0
  tell application appName
    activate
    repeat while not frontmost
      if elapsedSoFar > TMOUT then error "'" & appName & "' failed to activate within " & TMOUT & " seconds."
      delay 0.1
      set elapsedSoFar to elapsedSoFar + 0.1
    end repeat
  end tell
end syncActivate

# Closes a Terminal window after waiting for it to no longer be busy.
# Times out with an error after the specified number of seconds.
# The raison d'être for this handler is that blindly trying to close a Terminal window that's running a process (other than the shell) will result in an asynchronous, interactive 'really close?' prompt.
on closeTerminalWindow(win, timeoutSecs)
  local elapsedSoFar
  set elapsedSoFar to 0
  tell application "Terminal"
    repeat while (busy of selected tab of win)
      if elapsedSoFar ≥ timeoutSecs then
        error "Terminal window '" & (name of win) & "' is unexpectedly still busy after " & timeoutSecs & " seconds."
      end if
      delay 0.1
      set elapsedSoFar to elapsedSoFar + 0.1
    end repeat
    delay 0.5 # just for good measure - apparently, `busy` sometimes prematurely reports false
    close win
  end tell
end closeTerminalWindow

